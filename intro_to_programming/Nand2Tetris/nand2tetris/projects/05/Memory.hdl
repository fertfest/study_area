// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Put your code here:
    Not (in=address[14], out=notAddr14);
    And (a=load, b=notAddr14, out=loadRAM);
    And (a=load, b=address[14], out=loadAndaddress14);
    Not (in=address[13], out=notAddr13);
    And (a=loadAndaddress14, b=notAddr13, out=loadScreen);


    RAM16K (in=in, address=address[0..13], load=loadRAM, out=outRAM);
    Screen (in=in, address=address[0..12], load=loadScreen, out=outScreen);
    Keyboard (out=outKeyboard);
    
    Or8Way (in=address[0..7], out=orKey0to7);
    Or8Way (in[0..4]=address[8..12], in[5..7]=false, out=orKey8to12);
    Or (a=orKey0to7, b=orKey8to12, out=orKey);
    Mux16 (a=outKeyboard, b[0..15]=false, sel=orKey, out=keyboardOrnull);


    Mux16 (a=outScreen, b=keyboardOrnull, sel=address[13], out=screenOrKeyboard);
    Mux16 (a=outRAM, b=screenOrKeyboard, sel=address[14], out=out);
}